bool l_gameStarted;
bool l_inGame;
bool l_endRevealed;

int l_nbRevealedCards;
int l_revealedCard1;
int l_revealedCard2;
int l_rand;
int i;
int l_nbTries;
int l_totalRevealedCards;
int l_nbMaxErrors;

vector3 l_minPitch;
vector3 l_maxPitch;
vector3 l_currentPitch;

string l_sequence;
string l_strCard;
string l_name;

object l_revealedCard;

void UpdateErrors()
{
	OverlayElement_GetByName("Score/NbErreursText")->SetCaption( itoa( l_nbTries) + " / " + itoa( l_nbMaxErrors - 1));
};

void SetCardStr()
{
	l_strCard = itoa( l_rand);
	if (l_rand == 10)
	{
		l_strCard = "A";
	}
	elseif (l_rand == 11)
	{
		l_strCard = "B";
	}
	elseif (l_rand == 12)
	{
		l_strCard = "C";
	};
};

void ShuffleCards()
{
	l_nbRevealedCards = 0;
	l_sequence->clear();
	for (i = 1 ; i <= 12 ; i++)
	{
		l_rand = rtoi( rand( 1.5, 12.5));
		l_rand = rtoi( rand( 1.5, 12.5));
		l_rand = rtoi( rand( 1.5, 12.5));
		l_rand = rtoi( rand( 1.5, 12.5));
		l_rand = rtoi( rand( 1.5, 12.5));
		l_rand = rtoi( rand( 1.5, 12.5));
		l_rand = rtoi( rand( 1.5, 12.5));
		l_rand = rtoi( rand( 1.5, 12.5));
		SetCardStr();
		while (l_sequence->find( l_strCard) != npos)
		{
			l_rand = rtoi( rand( 1.5, 12.5));
			SetCardStr();
		};
		l_sequence = l_sequence + l_strCard;
		Object_GetByName( "Carte_" + itoa( l_rand))->SetParent( SceneNode_GetByName( "Carte_" + itoa( i)));
	};
	Echo( l_sequence);
};

void StopGame()
{
	Overlay_Hide( "InGame/Score");
	KazuGuiStop();
	l_gameStarted = false;
	l_inGame = false;
	Timer_KillAll();
	SceneNode_GetByName( "Carte_1")->SetOrientation( SetQuaternion( SetVector3( 1.0, 1.0, 1.0), 0.0));
	SceneNode_GetByName( "Carte_2")->SetOrientation( SetQuaternion( SetVector3( 1.0, 1.0, 1.0), 0.0));
	SceneNode_GetByName( "Carte_3")->SetOrientation( SetQuaternion( SetVector3( 1.0, 1.0, 1.0), 0.0));
	SceneNode_GetByName( "Carte_4")->SetOrientation( SetQuaternion( SetVector3( 1.0, 1.0, 1.0), 0.0));
	SceneNode_GetByName( "Carte_5")->SetOrientation( SetQuaternion( SetVector3( 1.0, 1.0, 1.0), 0.0));
	SceneNode_GetByName( "Carte_6")->SetOrientation( SetQuaternion( SetVector3( 1.0, 1.0, 1.0), 0.0));
	SceneNode_GetByName( "Carte_7")->SetOrientation( SetQuaternion( SetVector3( 1.0, 1.0, 1.0), 0.0));
	SceneNode_GetByName( "Carte_8")->SetOrientation( SetQuaternion( SetVector3( 1.0, 1.0, 1.0), 0.0));
	SceneNode_GetByName( "Carte_9")->SetOrientation( SetQuaternion( SetVector3( 1.0, 1.0, 1.0), 0.0));
	SceneNode_GetByName( "Carte_10")->SetOrientation( SetQuaternion( SetVector3( 1.0, 1.0, 1.0), 0.0));
	SceneNode_GetByName( "Carte_11")->SetOrientation( SetQuaternion( SetVector3( 1.0, 1.0, 1.0), 0.0));
	SceneNode_GetByName( "Carte_12")->SetOrientation( SetQuaternion( SetVector3( 1.0, 1.0, 1.0), 0.0));
};

void PauseGame()
{
	Timer_PauseAll();
	KazuGuiPause();
	Overlay_Hide( "InGame/Score");
	l_inGame = false;
};

void ContinueGame()
{
	Timer_PlayAll();
	KazuGuiResume();
	Overlay_Show( "InGame/Score");
	l_inGame = true;
};

void BeginGame()
{
	KazuGuiBegin();
	Overlay_Show( "InGame/Score");
	l_gameStarted = true;
	l_inGame = true;
	l_nbTries = 0;
	l_totalRevealedCards = 0;
	UpdateErrors();
	ShuffleCards();
	l_endRevealed = true;
};

void EndGame()
{
	KazuGuiStop();
	Overlay_Hide( "InGame/Score");
	l_gameStarted = false;
	l_inGame = false;
	_submitHighscore();
	timer( "EndGameAndCheckRS", TIMERTYPE_ONCE, 3.0)
	{
		Overlay_Hide( "WinGame");
		Overlay_Hide( "LoseGame");
		Overlay_Show( "Menu");
		OverlayElement_GetByName( "Menu/Jouer")->Hide();
		OverlayElement_GetByName( "Menu/Continuer")->Hide();
		OverlayElement_GetByName( "Menu/Recommencer")->Show();
	};
};

void RevealCard()
{
	if (l_revealedCard->GetParent()->GetOrientation()->GetAngle() == 0.0)
	{
		l_nbRevealedCards = l_nbRevealedCards + 1;
		l_name = l_revealedCard->GetName();
		strSplitOne( l_name, "_");
		if (l_nbRevealedCards == 1)
		{
			l_revealedCard1 = atoi( l_name);
		}
		else
		{
			l_revealedCard2 = atoi( l_name);
		};
		Sound_Play_Global( "Card");
		l_endRevealed = false;
		timer( "RevealCard", TIMERTYPE_CONTINUOUS, 1.0)
		{
			l_currentPitch = V3_LinearInterpolation( l_minPitch, l_maxPitch, CURRENTTIMER_TIME_LEFT);
			l_revealedCard->GetParent()->SetOrientation( SetQuaternion( SetVector3( 1.0, 0.0, 0.0), l_currentPitch[0]));
		};

		if (l_nbRevealedCards == 2)
		{
			l_nbRevealedCards = 0;
			timer( "EndGame", TIMERTYPE_ONCE, 1.0)
			{
				if ( rtoi( abs( itor( l_revealedCard1) - itor( l_revealedCard2))) == 6)
				{
					l_endRevealed = true;
					l_totalRevealedCards = l_totalRevealedCards + 2;
					if (l_totalRevealedCards == 12)
					{
						g_score = (l_nbMaxErrors - l_nbTries) * 1000;
						Echo( itoa( g_score));
						Overlay_Show( "WinGame");
						EndGame();
					};
				}
				else
				{
					l_nbTries = l_nbTries + 1;
					if (l_nbTries < l_nbMaxErrors)
					{
						UpdateErrors();
						l_endRevealed = false;
						timer( "UnrevealCards", TIMERTYPE_CONTINUOUS, 1.0)
						{
							l_currentPitch = V3_LinearInterpolation( l_minPitch, l_maxPitch, CURRENTTIMER_TIME_ELAPSED);
							Object_GetByName( "Carte_" + itoa( l_revealedCard1))->GetParent()->SetOrientation( SetQuaternion( SetVector3( 1.0, 0.0, 0.0), l_currentPitch[0]));
							Object_GetByName( "Carte_" + itoa( l_revealedCard2))->GetParent()->SetOrientation( SetQuaternion( SetVector3( 1.0, 0.0, 0.0), l_currentPitch[0]));
						};
						timer( "EndUnrevealCard", TIMERTYPE_ONCE, 1.0)
						{
							l_endRevealed = true;
						};
					}
					else
					{
						Overlay_Show( "LoseGame");
						EndGame();
					};
				};
			};
		}
		else
		{
			timer( "EndRevealCard", TIMERTYPE_ONCE, 1.0)
			{
				l_endRevealed = true;
			};
		};
	};
};

void ChooseCard()
{
	if (l_endRevealed)
	{
		l_revealedCard = MousePickupObject( MOUSE_POSITION_X, MOUSE_POSITION_Y, false);
		if (l_revealedCard != NULL && l_revealedCard->GetName()->find( "Carte_") != npos)
		{
			RevealCard();
		};
	};
};

void InitialiseGratzPaire()
{
	l_endRevealed = true;
	l_nbMaxErrors = 5;
	l_minPitch = VECTOR3_ZERO;
	l_maxPitch = SetVector3( 180.0, 0.0, 0.0);

	Overlay_Hide( "InGame/Score");
	Overlay_Hide( "WinGame");
	Overlay_Hide( "LoseGame");

	Sound_Play_Global( "Ambiance");

	OverlayElement_GetByName( "Commandes/Text")->SetCaption( "Cliquez sur les cartes que vous desirez retourner." );
	OverlayElement_GetByName( "Regles/Text")->SetCaption( "Retournez les cartes et essayez de trouver toutes les paires en commettant moins de 5 erreurs." );
	OverlayElement_GetByName( "Title")->SetMaterial( "TitreGratzPaire" );
	KazuGuiInit();
};

InitialiseGratzPaire();
